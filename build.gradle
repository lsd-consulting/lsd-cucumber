plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.20'
    id("org.jetbrains.dokka") version "1.8.20"
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'com.palantir.git-version' version '0.12.3'
    id 'jacoco'
}

apply from: 'hooks.gradle'
apply from: 'lsdReportVerification.gradle'

group = 'io.github.lsd-consulting'
version = gitVersion().replaceAll("^v", "")

repositories {
    mavenLocal()
    mavenCentral()
}

kotlin {
    jvmToolchain(11)
}

dependencies {
    api 'io.github.lsd-consulting:lsd-core:6.1.9'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    implementation 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    implementation 'com.j2html:j2html:1.6.0'
    implementation 'io.cucumber:cucumber-plugin:7.12.1'

    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'com.approvaltests:approvaltests:18.6.0'
    testImplementation 'org.mockito:mockito-core:5.4.0'
    testImplementation 'io.cucumber:cucumber-java8:7.12.1'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.12.1'
    testImplementation 'org.junit.platform:junit-platform-suite-engine:1.9.3'
}

jacocoTestReport {
    reports {
        xml.enabled(true)
        html.enabled(true)
        html.setDestination(project.provider(() -> new File("${project.buildDir}/reports/coverage")))
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
    finalizedBy jacocoTestReport
}

java {
    withSourcesJar()
}

tasks.register('javadocJar', Jar) {
    dependsOn(dokkaJavadoc)
    archiveClassifier = 'javadoc'
    from dokkaHtml.outputDirectory
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "$group"
            artifactId = 'lsd-cucumber'
            version = "${version}"

            from components.kotlin
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'lsd-cucumber'
                description = 'A library providing a Cucumber plugin for including Cucumber scenarios in the LSD reports.'
                url = 'https://github.com/lsd-consulting/lsd-cucumber.git'
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/lsd-consulting/lsd-cucumber/blob/main/LICENSE"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        name = "Nick"
                        email = "nicholas.mcdowall@gmail.com"
                        organization = 'NKM IT Solutions'
                        organizationUrl = 'https://github.com/nickmcdowall'
                    }
                    developer {
                        name = "Lukasz"
                        email = "lukasz.gryzbon@gmail.com"
                        organization = 'Integreety Ltd.'
                        organizationUrl = 'https://www.integreety.co.uk'
                    }
                }
                scm {
                    url = "https://github.com/lsd-consulting/lsd-cucumber.git"
                }
            }
            repositories {
                maven {
                    name = 'sonatype'
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                    credentials(PasswordCredentials)
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    if (project.findProperty("signingKey")) {
        // Use in-memory ascii-armored keys
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    } else {
        // Use signing properties in ~/.gradle/gradle.properties
        sign publishing.publications.mavenJava
    }
}
